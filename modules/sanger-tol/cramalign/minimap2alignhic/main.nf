process CRAMALIGN_MINIMAP2ALIGNHIC {
    tag "$meta.id"
    label "process_high"

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://community-cr-prod.seqera.io/docker/registry/v2/blobs/sha256/65/65858e733832166824cfd05291fc456bdf219b02baa3944c2c92efad86a6ee7f/data' :
        'community.wave.seqera.io/library/htslib_minimap2_samtools_gawk_perl:6729620c63652154' }"

    input:
    tuple val(meta),  path(cram),  path(crai), val(rglines)
    tuple val(meta2), path(index), path(reference)
    tuple val(chunkn), val(range)

    output:
    tuple val(meta), path("*.bam"), emit: bam
    tuple val("${task.process}"), val('minimap2'), eval('minimap2 --version | sed "s/minimap2 //g"'), emit: versions_minimap2, topic: versions
    tuple val("${task.process}"), val('gawk'), eval('gawk --version | grep -o -E "[0-9]+(\\.[0-9]+)+" | head -n1'), emit: versions_gawk, topic: versions
    tuple val("${task.process}"), val('filter_five_end.pl'), eval('echo 1.0'), emit: versions_filterfiveend, topic: versions
    tuple val("${task.process}"), val('samtools'), eval('samtools --version | head -1 | sed -e "s/samtools //"'), emit: versions_samtools, topic: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    // WARNING: This module includes the filter_five_end.pl script as a module binary in
    // ${moduleDir}/resources/usr/bin/filter_five_end.pl. To use this module, you will
    // either have to copy this file to ${projectDir}/bin or set the option
    // nextflow.enable.moduleBinaries = true
    // in your nextflow.config file.
    def args  = task.ext.args  ?: ''
    def args2 = task.ext.args2 ?: '-t' // copy RG, BC and QT tags to the FASTQ header line
    def args3 = task.ext.args3 ?: ''
    def args4 = task.ext.args4 ?: ''
    def args5 = task.ext.args5 ?: ''
    def args6 = task.ext.args6 ?: ''
    def prefix  = task.ext.prefix ?: "${cram}.${chunkn}.${meta.id}"
    // Prepare read group arguments if rglines are found, else, empty string
    def rg_arg = rglines ? '-y ' + rglines.collect { line ->
            // Add SM when not present to avoid errors from downstream tool (e.g. variant callers)
            def l = line.contains("SM:") ? line : "${line}\tSM:${meta.id}"
            "-R '${l.replaceAll("\t", "\\\\t")}'"
        }.join(' ')
        : ''
    """
    samtools view --no-PG -H ${cram} | grep ^@PG > ${prefix}_cram_pg.tmp

    samtools cat ${args} -r "#:${range[0]}-${range[1]}" ${cram} |\\
        samtools fastq ${args2} - |\\
        minimap2 -t${task.cpus} ${args3} ${index} ${rg_arg} - |\\
        insert_cram_pg_header.awk -v pgfile="${prefix}_cram_pg.tmp" |\\
        gawk -F'\t' '
            BEGIN { OFS="\\t" }
            \$1 ~ /^\\@/ { print \$0 }
            \$1 !~ /^\\@/ && and(\$2, 64) > 0 { print 1 \$0 }
            \$1 !~ /^\\@/ && and(\$2, 64) == 0 { print 2 \$0 }
        ' |\\
        filter_five_end.pl |\\
        gawk '
            BEGIN { FS = OFS="\\t" }
            \$1 ~ /^\\@/ { print \$0 }
            \$1 !~ /^\\@/ { \$2 = and(\$2, compl(2048)); print substr(\$0, 2) }
        ' |\\
        samtools fixmate ${args4} - - |\\
        samtools view -h ${args5} |\\
        samtools sort ${args6} -@${task.cpus} -T ${prefix}_tmp -o ${prefix}.bam -
    """

    stub:
    def prefix  = task.ext.prefix ?: "${cram}.${chunkn}.${meta.id}"
    """
    touch ${prefix}.bam
    """
}
