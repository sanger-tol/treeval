process CRAMALIGN_BWAMEM2ALIGNHIC {
    tag "$meta.id"
    label "process_high"

    conda "${moduleDir}/environment.yml"
    container "${ workflow.containerEngine == 'singularity' && !task.ext.singularity_pull_docker_container ?
        'https://community-cr-prod.seqera.io/docker/registry/v2/blobs/sha256/e0/e05ce34b46ad42810eb29f74e4e304c0cb592b2ca15572929ed8bbaee58faf01/data' :
        'community.wave.seqera.io/library/bwa-mem2_htslib_samtools:db98f81f55b64113' }"

    input:
    tuple val(meta),  path(cram),  path(crai), val(rglines)
    tuple val(meta2), path(index), path(reference)
    tuple val(chunkn), val(range)

    output:
    tuple val(meta), path("*.bam"), emit: bam
    tuple val("${task.process}"), val('bwamem2'), eval('bwa-mem2 version 2>| grep -o -E "[0-9]+(\\.[0-9]+)+"'), emit: versions_bwamem2, topic: versions
    tuple val("${task.process}"), val('samtools'), eval('samtools --version | head -1 | sed -e "s/samtools //"'), emit: versions_samtools, topic: versions

    when:
    task.ext.when == null || task.ext.when

    script:
    // WARNING: This module includes insert_cram_pg_header as a module binary in
    // ${moduleDir}/resources/usr/bin/insert_cram_pg_header. To use this module, you will
    // either have to copy this file to ${projectDir}/bin or set the option
    // nextflow.enable.moduleBinaries = true
    // in your nextflow.config file.
    def args  = task.ext.args  ?: ''
    def args2 = task.ext.args2 ?: '-t' // copy RG, BC and QT tags to the FASTQ header line
    def args3 = task.ext.args3 ?: ''
    def args4 = task.ext.args4 ?: ''
    def args5 = task.ext.args5 ?: ''
    def args6 = task.ext.args6 ?: ''
    def prefix  = task.ext.prefix ?: "${cram}.${chunkn}.${meta.id}"
    // Prepare read group arguments if rglines are found, else, empty string
    def rg_arg = rglines ? '-C ' + rglines.collect { line ->
            // Add SM when not present to avoid errors from downstream tool (e.g. variant callers)
            def l = line.contains("SM:") ? line : "${line}\tSM:${meta.id}"
            "-H '${l.replaceAll("\t", "\\\\t")}'"
        }.join(' ')
        : ''
    // Please be aware one of the tools here required mem = 28 * reference size!!!
    """
    INDEX=`find -L ./ -name "*.amb" | sed 's/\\.amb\$//'`

    samtools view --no-PG -H ${cram} | grep ^@PG > ${prefix}_cram_pg.tmp

    samtools cat ${args} -r "#:${range[0]}-${range[1]}" ${cram} |\\
        samtools fastq ${args2} - |\\
        bwa-mem2 mem ${args3} -t ${task.cpus} \${INDEX} ${rg_arg} - |\\
        insert_cram_pg_header.awk -v pgfile="${prefix}_cram_pg.tmp" |\\
        samtools fixmate ${args4} - - |\\
        samtools view -h ${args5} |\\
        samtools sort ${args6} -@${task.cpus} -T ${prefix}_tmp -o ${prefix}.bam

    rm ${prefix}_cram_pg.tmp
    """

    stub:
    def prefix  = task.ext.prefix ?: "${cram}.${chunkn}.${meta.id}"
    """
    touch ${prefix}.bam
    """
}
